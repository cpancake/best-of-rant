# FPP Conversation Simulator

[$[ppl]:{
    killowatt|Berkin|atlantique|CmdrMatthew|Cold|
    commie|cra0kalo|Dragon Robot|farm|Layla|
    rustlord420|Shoar|Swebonny|traffic|Vbitz|
    Handsome Matt|Tamschi|Spans|dajoh|krix|toaster|jos|*0.001*garry
}]
[$[nick]:{
    killo|berk{|in}|atlantique|cmdr|cold|
    commie|cra0|derg{|bot}|farm|layla|
    rustlord|shoar|sweb|traffic|vbitz|
    Matt|Tamschi|Spans|dajoh|krix|sbg|jos|*0.001*garry
}]

# Time shit
``time=1440+[num:0;1440]``
[$[timestamp]:`hours=(time/60)-(time/60%1);(hours-1)%12+1`\:``mins=time%60``[cmp:`mins`;10;[is:less;0]]`mins`\s[cmp:`time-1440`;720;[is:greater equal;P][is:less;A]]M]

[$[h_msg:user]:
	# Timestamp and author
    [case:none][$timestamp] ‑ [arg:user]:\s
    # Update time
	``time=(time-1440+[num:0;{*8*1|*2*3|5}])%1440+1440``
]
[$[h_me:user]:
	# Timestamp and author
    [case:none][$timestamp] ‑ [arg:user]\s
    # Update time
	``time=(time-1440+[num:0;{*8*1|*2*3|5|*0.01*[num:5;30]}])%1440+1440``
]

[r:100][s:\n]
[before:
	``i=[ri]`` # Store the repeater index to make things a LOT simpler
    
    # Current author
	[$h_msg:[b:author`i`;[$ppl]]]
    
    # Randomize capitalization of message
    [case:{*8*none|upper|first}]
]
[after:{*3*|[r:[n:1;3]]{!}}] # Randomize ending punctuation

# Message body
{
	# Talk shit about/shamelessly praise a language
    [b:lang`i`;{use\s|}]
    {C\#|mond|c++|lua|rant|jet|php|java|*0.1*haskell}
    [b:lang`i`;{|\s{best|worst|sucks}}]|
    
    # "X me"
    [b:do_me_what`i`;<verb-transitive>] me
    {
    	# If it's directed at a user, say their name
    	\s[b:do_me_who`i`;[$nick]]\n
        
        	# They deliver
    		[$h_me:[b:do_me_who`i`;[$ppl]]]
        	[b:do_me_what`i`;<verb.s-transitive>] [b:author`i`;[$nick]]
            
            # Optional reaction
            {|\n[$h_msg:[b:author`i`;[$ppl]]]{thanks|thx|<vocal>|<x>}}
    |}|
    
    # X is Y
    [$nick] is <adj>|
    
    # Say hello
    <greet> [$nick]|
    
    # Ayy lmao
    {{<greet>|*0.2* ayyy lmao|*0.2*ayyy} [$nick]}|
    
    # "X my Y"
    <verb-transitive> my <noun-body>|
    
    # Reaction or greeting
    [notfirst:{^|:v|ye|wow}[chance:0]][first:<greet>]|
    
    # Insult someone
    fu{c|}k {you|me|{{you|off}\s|}[$nick]|<noun.plural>}|
    
    # X please
    [$nick] {plz|please}|
    
    # Laugh erratically
    *0.5* [r:[n:2;10]]{hue|ha|he|hee}|
    
    # Exclamation
    <x>|
    
    # Post a link
    [case:none]"http://"
    {
    	"i.imgur.com/"[r:5]{\w|\W}.jpg|
        *0.01*"redtube.com/"\5,D
    }{|*0.1*\s{nsfw|[$nick] {check this out|look at this|here's my <noun>}}}[chance:0]| 
}
